[toc]

80386是x86微处理器家族发展中的里程碑，它不仅兼容先前的8086/8088和80286等微处理器，而且也为后来的80486、Pentium和PentiumPro等微处理器奠定了基础。80386支持实方式和保护方式两种运行模式。在实方式下，80386相当于一可进行32位处理的快速8086,原先为8086/8088设计的程序几乎都可适用于80386。当开机或者经硬件RESET线重新初始化时，80386处于实方式下。本章介绍80386寄存器、寻址方式和指令集，这些内容是进行80386程序设计的基础，对实方式和保护方式都有效。

# 9.1 80386寄存器

80386寄存器的宽度大多是32位，可分为如下几组：==通用寄存器、段寄存器、指令指针及标志寄存器、系统地址寄存器、控制寄存器、调试寄存器和测试寄存器==。应用程序主要使用前三组寄存器，只有系统程序才会使用各种寄存器。这些寄存器是x86系列微处理器

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.6iufiyrahqo0.webp)

先前成员(8086/8088、80186和80286)寄存器的超集，所以80386包含了先前微处理器的全部16位寄存器。8086/8088没有系统地址寄存器和控制寄存器等寄存器。

## 9.1.1 通用寄存器

==80386有8个32位通用寄存器，这8个寄存器分别定名为EAX、ECX、EDX、EBX、ESP、EBP、ESI和EDI。它们是原先的16位通用寄存器的扩展==，请参见图9.1。这些通用寄存器的低16位可以作为16位的寄存器独立存取，并把它们分别定名为AX、CX、DX、BX、SP、BP、SI和DI，它们也就是x86系列微处理器先前成员的8个16位通用寄存器。在存取这些16位的寄存器时，相应的32位通用寄存器的高16位不受影响。与先前的微处理器一样，AX、BX、CX和DX这4个16位的数据寄存器的高8位和低8位可以被独立存取，分别命名为AH、AL、BH、BL、CH、CL、DH和DL。在存取这些8位寄存器时，相应的16位寄存器的其它位不受影响，相应的通用寄存器的其它位也不受影响。由此可见，80386在扩展先前微处理器寄存器组时，为执行8086和80286等微处理器代码提供了兼容的寄存器组。

这些32位通用寄存器不仅可以传送数据、暂存数据、保存算术或逻辑运算的结果，而且还可以在基址和变址寻址时，存放地址。例如：

```assembly
MOV 	EAX,12345678H
MOV 	[EBX],EAX
ADD 	EAX,[EBX+ESI+1]
MOV 	AL,[ECX+EDI+1234]
SUB		CX,[EAX-12]
```

在先前的微处理器中，只有BX、BP、SI和DI可以在基址和变址寻址时存放地址，而现在==80386的8个32位通用寄存器都可以作为指针寄存器使用，所以说这些32位通用寄存器更具有通用性==。

## 9.1.2 段寄存器

80386有6个16位段寄存器，分别定名为CS、SS、DS、ES、FS和GS。在实方式下，代码段寄存器CS、堆栈段寄存器SS、数据段寄存器DS和附加段寄存器ES的功能与先前微处理器中对应段寄存器的功能相同。==FS和GS是80386新增加的段寄存器==。因此80386上运行的程序可同时访问多达6个段。

在实方式下，内存单元的逻辑地址仍是`段值:偏移`形式。为了访问一个给定内存段中的数据，可直接把相应的段值装入某个段寄存器中。例如：

```assembly
MOV 	AX,SEG BUFFER
MOV 	FS,AX
MOV 	AX,FS:[BX]
```

在保护方式下，情况要复杂得多，装入段寄存器的不再是段值，而是称为选择子的某个值。有关内容在第10章介绍。

## 9.1.3 指令指针和标志寄存器

80386的指令指针和标志寄存器分别是先前微处理器的指令指针IP和标志寄存器FLAG的32位扩展。

### 1. 指令指针寄存器

80386的指令指针寄存器扩展到32位，记为EIP。EIP的低16位是16位的指令指IP,它与先前微处理器中的IP相同。IP寄存器提供了用于执行8086和80286代码的指令指针。==由于实方式下段的最大范围是64K，所以EIP中的高16位必须是0,仍相当于只有低16位的IP起作用。==

### 2. 标志寄存器

==80386的标志寄存器也扩展到32位，记为EFLAGS==，如图9.2所示。与8086/8088的16位标志寄存器相比，增加了4个控制标志，其他标志位的位置和意义均与8086/8088相同。下面简单介绍这4个控制标志，它们在实方式下不发挥作用

#### (1)IO特权标志IOPL(I/O Privilege Level)

==IO特权标志有2位宽，也称为IO特权级字段。IOPL字段指定了要求执行1/O指令的特权级==。如果当前的特权级别在数值上小于或等于IOPL,那么1/O指令可执行，否则发生一个保护异常。在80286的16位标志寄存器中，已含有该标志。

#### (2)嵌套任务标志NT(Nested Task)

==联套任务标志控制中断返回指令IRET的执行。==如果NT=0,用堆栈中保存的值恢复EFLAGS、CS和EIP执行常规的从中断返回的动作。如果NT=1,通过任务转换实中断返回。在80286的16位标志寄存器中，已含有该标志。

#### (3)重启动标志RF(Restart Flag)

==重启动标志控制是否接受调试故障==。RF=0接受，RF=1忽略。在成功地完成每一条指令后，处理器把RF清0。而当接收到一个非调试故障时，处理器把RF置1。

#### (4)虚拟8086方式标志VM(Virtual 8086 Mode)

==如果该标志置为1,处理器将在虚拟的8086方式下工作，如果清0，处理器工作在一般的保护方式下==。

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.1su3t0tjhczk.webp)

# 9.2 80386存储器寻址

80386支持先前微处理器的各种寻址方式。在立即寻址方式和寄存器寻址方式中，操作数可达32位宽。在存储器（内存）寻址方式中，不仅操作数可达32位，而且寻址范围和方式更加灵活。本节介绍80386存储器寻址方式。

## 9.2.1 存储器寻址基本概念

==80386继续采用分段的方法管理主存储器。存储器的逻辑地址由段基地址（段起始址）和段内偏移两部分表示，存储单元的地址由段基地址加上段内偏移所得。段寄存器指示段基地址，各种寻址方式决定段内偏移。==

==在实方式下，段基地址仍然是16的倍数，段的最大长度仍然是64K。段寄存器内所的仍然是段基地址对应的段值，存储单元的物理地址仍然是段寄存器内的段值乘上16加上段内偏移。所以，尽管80386有32根地址线，可直接寻址物理地址空间达到4G字节，但在实方式下仍然与8086/8088相似。==

==在保护方式下，段基地址可以长32位，无须是16的倍数，段的最大长度可达4G。段寄存器内所含的是指示段基地址的选择子，存储单元的地址是段基地址加上段内偏移，==但不再是段寄存器之值乘16加上偏移，这与8086/8088完全不同。

==段寄存器指示段基地址。在实方式下，段寄存器含段值，直接指示段基地址==；==在保护方式下，段寄存器含选择子，间接指示段基地址。==每次对存储器的访问或是隐含地、或是显式地、或是默认地指定了某个段寄存器。==由于80386有6个段寄存器，所以在某一时刻程序可访问6个段，而不再是先前的4个段。==

由段寄存器CS所指定的段称为当前代码段。80386在取指令时，自动引用代码段寄存器CS。80386的指令指针寄存器是EIP。==通常可以认为CS和EIP指示下一条要执行的指令==。在实方式下，由于段的最大长度不超过64K,所以EIP的高16位为0,相当于IP。

==由段寄存器SS所指定的段称为当前堆栈段==。==80386在访问堆栈时，总是引用堆栈段寄存器SS==。==在实方式下，80386把ESP的低16位SP作为指向栈顶的指针，可以认为堆栈顶由SS和SP指定==。==在保护方式下，32位堆栈段的堆栈指针是ESP,16位堆栈段的堆栈指针是SP==。==如果要访问存储在堆栈中的数据，也可以通过引用SS段寄存器进行。==

DS 寄存器是主要的数据段寄存器，对于访问除堆栈外的数据段它是一个默认的段寄存器。在以BP或EBP或ESP作为基址寄存器访问堆栈时，默认的段寄存器是SS。某些字符串操作指令总是使用ES段寄存器作为目标操作数的段寄存器。此外，尽管CS、SS、ES、FS和GS都可作为访问数据时引用的段寄存器，但必须显式地在指令中指定，它们也即成为段超越前级，这使得指令在长度和执行时间上的开销稍大一些。例如：

```assembly
MOV 	EAX,[SI]	;歌认段寄存器DS
MOV		[BP+2],EAX	;默认段寄存器SS
MOV		AL,FS:[BX]	;显式指定段寄存器FS
MOV 	GS:[BP],DX	;显式指定段寄存器GS
```

一般说来，使DS含有最经常访问的数据段的段值，而用ES、FS和GS含有那些不经常使用的数据段的段值。当然，32位数据仍按`高高低低`原则存取。指令`MOV EAX,[1234H]`的传送示意如图9.3所示。

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.3tam3wp3w4c0.webp)

## 9.2.2 灵活的存储器寻址方式

==各种存储器寻址方式表示的是有效地址，也即段内偏移。在实方式下，段内偏移不能超过64K；在保护方式下，段内偏移可以超过64K==。80386既支持各种先前16位偏移的寻址方式，又增加了灵活的32位偏移的寻址方式。

80386支持先前微处理器所支持的各种存储器寻址方式。先前微处理器的存储器寻址方式可分为五种，其中相对基址加变址寻址方式最复杂。==相对基址加变址寻址方式中的16位有效地址（偏移）由三部分相加构成：一个基址寄存器（只能是BX或BP寄存器）、一个变址寄存器（只能是SI或DI寄存器）和一个常数偏移量（最大16位）。==在这三部分中如果没有常数偏移量，那么就成为基址加变址寻址；如果没有基址寄存器或者变址寄存器那么就成为寄存器相对寻址；如果只有基址寄存器或者只有变址寄存器，没有相对偏移量，那么就成为寄存器间接寻址；如果既没有基址寄存器也没有变址寄存器，只剩下常数偏移量，那么就成为直接寻址。80386支持上述各种16位偏移的存储器寻址方式。

==80386还支持32位偏移的存储器寻址方式。80386允许内存地址的偏移可以由三部分内容相加构成：一个32位基址寄存器，一个可乘上比例因子1、2、4或8的32位变址寄存器，及一个8位或32位的常数偏移量。==并且这三部分可省去任意的两部分。例如：

```assembly
MOV 	AL,ES:[5678H]
MOV 	CX,[EBX]
MOV 	EDX,[EBX+ED1+1234H]
MOV 	AX,[EBX+ESI*4]
MOV 	ESI,[EBX*8+100H]
MOV 	BH,ES:[EBX+EDI*8+6]		;显式指定段寄存器ES
```

如果含变址寄存器，那么变址寄存器中的值先按给定的比例因子放大，再加上偏移。在这些寻址方式中，8个32位通用寄存器都可作为基址寄存器使用，除了ESP寄存器外，其他7个通用寄存器都可作为变址寄存器使用。例如：

```assembly
MOV		AL,[ECX]
MOV 	BX,[EAX-4]
MOV 	[EDX+EDI],CX
MOV		[EBX+EAX*2],DH
MOV 	ESI,[EAX+ECX*8+1234H]
```

在所有导址方式中，对数据的访问所默认引用的段寄存器取决于所选择的基址寄存器。如果基址寄存器是ESP或者EBP,那么默认的段寄存器从通常的DS改为SS。对于别的基址寄存器的选择，包括没有基址寄存器的情况，DS仍然是默认的段寄存器。如9.2.1节中所述，访问由非默认的段寄存器指定的某个段的数据，要使用一额外的指令字节来指定所要的段寄存器。当EBP作为变址寄存器使用（ESP不能作为变址寄存器使用）时，不影响默认段寄存器的选择。默认段寄存器的选择只受所选的基址寄存器所影响。例如：

```assembly
MOV 	AL,[EBX+EBP*2]		;默认的段寄存器是DS
MOV 	AL,[EBX+EBP]		;默认的段寄存器是DS
MOV 	AL,[EBP+EBX]		;默认的段寄存器是SS
MOV 	AL,GS:[EBP*2]		;显式指定段寄存器GS
MOV 	EAX,[ESP]			;默认的段寄存器是SS
MOV 	AL,CS:[ESP+2]		;显式指定段寄存器CS
MOV 	[ESP+EBP*2],ECX		;默认的段寄存器是SS
MOV 	AL,DS:[ESP+EDI+12]	;显式指定段寄存器DS
```

==80386支持的32位偏移的存储器寻址方式可归纳如下==：
$$
\left\{
\begin{matrix}
无\\EAX\\ECX\\EDX\\EBX\\ESP\\EBP\\ESI\\EDI
\end{matrix}
\right\}
+
\left\{
\begin{matrix}
无\\EAX\\ECX\\EDX\\EBX\\...\\EBP\\ESI\\EDI
\end{matrix}
\right\}
\times
\left\{
\begin{matrix}
1\\2\\4\\8
\end{matrix}
\right\}
+
\left\{
\begin{matrix}
无\\8位\\32位
\end{matrix}
\right\}
$$
要特别说明的是，在实方式下，也可使用上述32位偏移的存储器寻址方式，但所得偏移不应超过OFFFFH,而且操作数的最高字节单元的地址偏移也不能超过OFFFFH。原因是实方式下段的长度是64K。但有一种特殊情况，可使得段超过64K,在这种特殊情况下，就要使用32位的偏移来访问超过偏移64K部分的存储单元。

==如果某一存储器操作数的地址是该操作数尺寸（长度）的倍数，那么称该操作数是对齐的。当段基地址是16的倍数时，存储器操作数是否对齐就取决于偏移。==例如：==如果一个字的偏移量是2的倍数，那么该字为对齐的；再如：一个双字的偏移量是4的倍数，那么该双字是对齐的。在80386中，如果存储器操作数是对齐的，那么访问它就比较快。当然，不对齐的操作数也能访问，但所费时间可能多一点点。程序员应该尽量使操作数对齐，以提高访问存储器操作数的速度。==

## 9.2.3 支持各种数据结构

80386支持的`基地址+变址+位移量`寻址方式能进一步满足高级语言支持的数据结构的需要。标量变量、记录、数组、记录的数组和数组的记录等数据结构可方便地利用80386的这种寻址方式实现。对FORTRAN而言，这些数据结构可作为Static存储来分配。对Pascal或者C而言，这些数据结构可以动态地分配在一个程序的堆栈或者堆中。基地址和变址寄存器为寻址方式提供两个动态的成分，而位移量提供静态的成分。用在一数据段中的一个常数位移量可以简单地寻址静态分配的数据。用一相对于ESP或者EBP寄存器的常数位移量可以寻址分配在堆栈中的数据。表9.1给出了高级语言的需要和80386支持的寻址方式之间的关系。

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.5aoqa0obnxc0.webp)

为了简化寄存器的分配，可以用8个通用寄存器中的一个作为基地址寄存器，以及用除ESP外的一个变址寄存器。变址寄存器的值可以直接地被使用(比例因子为1),或者按2、4或8的倍率用于16位、32位、64位变量变址，而不需要计算位移的指令或使用一额外的寄存器。

# 9.3 80386指令集

80386的指令集包含了8086/8088、80186和80286指令集。可分为如下大类：

- 数据传送指令
- 算术运算指令
- 逻辑运算和移位指令
- 控制转移指令
- 串操作指令
- 高级语言支持指令
- 条件字节设置指令
- 位操作指令
- 处理器控制指令
- 保护方式指令

高级语言支持指令始于80186。保护方式指令始于80286。条件字节设置指令和位操作指令等是80386新增的。

80386是32位处理器，80386指令的操作数长度可以是8位、16位或者32位。80386认为，32位操作数是对16位操作数的扩展。80386既支持16位的存储器操作数有效地址，又支持32位的存储器操作数有效地址。80386认为，32位存储器操作数有效地址是对16位存储器操作数有效地址的扩展。所以，80386支持的32位操作数的指令往往就是对相应支持16位操作数指令的扩展；80386的32位存储器操作数有效地址寻址方式往往就是对16位存储器操作数有效地址导址方式的扩展。

本节在第2章介绍的8086/8088指令集基础上介绍80386指令集，有关保护方式的指令在第10.8节中介绍。

## 9.3.1 数据传送指令

数据传送指令实现在寄存器、内存单元或1/0端口之间传送数据和地址。80386的数据传送指令仍分成四种：

1. 通用数据传送指令
2. 累加器专用传送指令
3. 地址传送指令
4. 标志传送指令

### 1. 通用传送指令组

80386的通用传送指令组含有如下十条指令

- 数值传送指令MOV
- 符号扩展指令MOVSX
- 零扩展指令MOVZX
- 交换指令XCHG
- 进栈指令PUSH、PUSHA、PUSHAD
- 退栈指令POP、POPA和POPAD

#### (1) 数值传送指令MOV

数值传送指令MOV的格式、功能和使用注意点都与8086/8088的MOV指令相同。==传送的数据可以是8位、16位或32位==。例如：

```assembly
MOV EAX,12345678H
MOV ESI,EDI
MOV [BX+SI+1],EBX
MOV CL,[EAX+EBX+3]
MOV FS,AX
MOV ES:[ECX+EDX*4],DX
MOV GS:DWORD PTR [1234H],4321H
```

#### (2) 符号扩展指令MOVSX和零扩展指令MOVZX(始于80386)

> MOVSX：高位补符号位
>
> MOVZX：高位补0

符号扩展指令的格式如下：

```assembly
MOVSX DST,SRC
```

该指令的功能是把源操作数SRC的内容送到目的操作数DST,目的操作数空出的位用源操作数的符号位填补。

零扩展指令的格式如下：

```assembly
MOVZX DST,SRC
```

==该指令的功能是把源操作数SRC的内容送到目的操作数DST,目的操作数空出的位用零填补==。

==符号扩展指令和零扩展指令中的目的操作数DST必须是16位或32位寄存器，源操作数SRC可以是8位或16位寄存器，也可以是8位或16位存储器操作数。如果源操作数和目的操作数都是字，那么就相当于MOV指令。==

==这两条指令不影响各标志==。

例如：

```assembly
MOV 	DL,92H
MOVSX 	AX,DL			;92H扩展成FF92H送AX
MOVSX 	EBX,DL			;92H扩展成FFFFFF92H送EBX
MOVZX 	CX,DL			;92H扩展成0092H送DX
MOVZX 	ESI,DL			;92H扩展成00000092H送ESI
MOV 	WORD PTR [BX],1234H
MOVSX 	ESI,WORD PTR [BX];1234H扩展成00001234H送ESI
MOVZX 	EDI,WORD PTR [BX];1234H扩展成00001234H送EDI
```

符号扩展指令MOVSX和零扩展指令MOVZX是80386新增的指令。使用MOVSX可以对有符号数进行扩展，显然MOVSX指令要比CBW指令和CWD指令的功能强。使用MOVZX可以方便地对无符号数进行扩展。

#### (3) 交换指令XCHG

交换指令XCHG的格式、功能和使用注意点都与8086/8088的XCHG指令相同。交换的数据可以是8位、16位或32位。例如：

```assembly
XCHG 	ESI,ED
XCHG 	[EBX+ESI*2+1],BX
XCHG 	CL,[EAX+EBX+3]
```

#### (4) 进栈指令PUSH

==进栈指令PUSH的格式没变，但功能增强了。从80186开始，压入堆栈的操作数还可以是立即数。从80386开始操作数长度还可以达32位，当然如果操作数长度是32位，那么堆栈指针减4。==

例如：

```assembly
PUSH 	EAX
PUSH 	DWORD PTR [BX]
PUSH	DWORD PTR [EAX]
PUSH 	FS
PUSH 	1234H			;16位立即数
PUSH 	12345678H		;32位立即数
```

==在调用通过堆栈传递入口参数的子程序时，把立即数直接压入堆栈的操作能方便地把常量作为参数传递给子程序==。例如：

```assembly
PUSH	OF000H	;压入立即数PUSH0压入立即数
CALL	ECHOBD	;调用子程序
ADD 	SP,4	;平衡堆栈
```

有一点要注意：当用PUSH指令把堆栈指针SP或ESP压入堆栈时，80386/80286的处理方式不同于8086/8088。==8086/8088是将SP减2后的值进栈，而80386/80286是将进栈操作前的SP(或ESP)值进栈。==

#### (5) 出栈指令POP

出栈指令POP的格式、功能和使用注意点都没变，除允许弹出32位操作数外。例如：

```assembly
POP 	EAX
POP		WORD PTR [ECX]
POP		DWORD PTR [BX]
```

#### (6 ) PUSHA POPA

==16位通用寄存器全进栈指令PUSHA和全出栈指令POPA==(始于80186)PUSHA指令和POPA指令提供了压入或弹出8个16位通用寄存器的有效手段，它们的一般格式如下：

```assembly
PUSHA
POPA
```

==PUSHA指令将所有8个通用寄存器（16位）内容压入堆栈，其顺序是：AX、CX、DX、BX、SP、BP、SI、DI,然后堆栈指针寄存器SP之值减16，所以SP进栈的内容是PUSHA执行之前的值。==

POPA指令从堆栈弹出内容以PUSHA相反的顺序送到这些通用寄存器，从而恢复PUSHA之前的寄存器内容。但堆栈指针寄存器ESP之值不是由堆栈弹出，而是通过增加16来恢复

==这两条指令不影响标志。==这两条指令都没有显式的操作数。

==在中断处理程序和子程序中，利用这两条指令能快速地进行现场保护和恢复==。例如：

```assembly
SUBX	PROC
		PUSHA
		...
		...
		POPA
		RET
SUBX 	ENDP
```

尽管PUSHA指令比8个独立的PUSH指令快，但它比3或4个独立的PUSH指令要慢，同时还将使SP减16,所以，如果只需要保存部分寄存器，那么仍以使用PUSH指令为妥。对POPA和POP指令而言也是如此。

#### (7) 32位通用寄存器全进栈指令PUSHAD和全出栈指令POPAD(始于80386)

==PUSHAD指令和POPAD指令提供了压入或弹出8个32位通用寄存器的有效手段==，它们的一般格式如下：

```assembly
PUSHAD
POPAD
```

==PUSHAD指令将所有8个通用寄存器（32位）内容压入堆栈，其顺序是：EAX、ECX、EDX、EBX、ESP、EBP、ESI、EDI,然后堆栈指针寄存器ESP之值减32,所以ESP进栈的内容是PUSHAD执行之前的值==。

==POPAD指令从堆栈弹出内容以PUSHAD相反的顺序送到这些通用寄存器，从而恢复PUSHAD之前的寄存器内容。但堆栈指针寄存器SP之值不是由堆栈弹出，而是通过增加32来恢复。==

==这两条指令不影响各标志==。这两条指令都没有显式的操作数。

这两条指令是PUSHA和POPA指令的扩展。

### 2. 地址传送指令组

#### (1) 装入有效地址指令LEA

> 装入有效地址

==装入有效地址指令的格式和功能没变。源操作数仍然必须是存储器操作数，目的操作数是16位或者32位通用寄存器。当目的操作数是16位通用寄存器时，那么只装入有效地址的低16位。==这符合实方式下的实际应用需要

例如：

```assembly
MOV 	EBX,12345678H		;置EBX之值
MOV 	ECX,87654321H		;置ECX之值
LEA 	ESI,[EBX+ECX+1234H]	;执行后ESI=9999ABCDH
LEA		DX,[EBX+ECX+1234H]	;执行后DX=0ABCDH
LEA 	EDI,[BX-3]			;执行后ED1=00005675H
```

利用该指令还可以进行简单的算术运算，例如

```assembly
LEA 	ECX,[EAX+EDX*2+1234H]	;ECX=EAX+EDX*2+1234H
LEA 	EDX,[EBX*8]				;DX=EBX*8
```

#### (2) 装入指针指令组

> ？不太清楚

装入指针指令组有5条指令，它们的格式如下：

```assembly
LDS 	REG,OPRD
LES		REG,OPRD
LFS 	REG,OPRD		;始于80386
LGS 	REG,OPRD		;始于80386
LSS 	REG,OPRD		;始于80386
```

==这些指令的功能将源操作数OPRD所指内存单元的4个或6个相继字节单元的内容送到指令助记符给定的段寄存器和目的操作数REG中。目的操作数必须是16位或32位（始于80386)通用寄存器，源操作数是存储器操作数。如果目的操作数是16位通用寄存器，那么源操作数OPRD含32位指针。如果目的操作数是32位通用寄存器，那么源操作数OPRD含48位指针，这适合于使用32位偏移的场合，可用于一次装载48位全指针。==

==这些指令不影响各标志。==

==用LDS、LES、LFS和LGS指令装入完整的指针是很方便的==。例如：

```assembly
STRING 	DB 	'HELLO'
PPSTR 	DD 	STRING
FPSTR 	DF 	STRING
WPSTR 	DW 	STRING
		DW 	SEG STRING
		......
		LFS 	DI,PPSTR
		LGS 	ESI,FPSTE
		LES 	DI,DWORD PTR WPSTR
```

==用LSS指令装载堆栈指针是简单安全的方法==，例如：

```assembly
LSS 	SP,SSPTR		;SSPTR是含有堆栈指针的双字
```

==它确保在一条指令中使SS和SP都被重置==。

### 3. 标志传送指令组

80386的标志传送指令组含有如下6条指令：==LAHF、SAHF、PUSHF、PUSHFD、POPF和POPFD。==

指令LAHF、SAHF、PUSHF和POPF指令格式和功能等均与8086/8088相同。

32位标志寄存器进栈和出栈指令的格式如下，它们始于80386:

```assembly
PUSHFD
POPFD
```

==PUSHFD指令将整个标志寄存器的内容压入堆栈；POPFD指令将栈顶的一个双字弹出到32位的标志寄存器中==。这两条指令是PUSHF和POPF指令的扩展。

==PUSHFD指令不影响各标志；POPFD指令将影响各标志。==

### 4. 累加器专用传送指令组

==80386累加器专用指令组含有如下指令：IN、OUT和XLAT。==

输入指令IN的格式和功能与8086/8088相同，但可以通过累加器EAX输入一个双字。例如：

```assembly
IN 	EAX,DX		;从DX规定的端口输入一个双字
IN 	EAX,20H		;从20H口输入一个双字
```

输出指令OUT的格式和功能与8086/8088相同，但可以通过累加器EAX输出一个双字。例如：

```assembly
OUT 	DX,EAX		;输出一个双字到DX规定的端口
OUT 	20H,EAX		;输出一个双字到20H口
```

表转换指令XLAT的格式和功能与8086/8088相同。但从80386开始存放基值的寄存器可以是EBX。也就是说，扩展的XLAT指令以EBX为存放基值的寄存器，非扩展的XLAT 指令以 BX 为存放基值的寄存器。

## 9.3.2 算术运算指令

80386算术运算指令的操作数可以扩展到32位，此外与8086/8088相比还增强了有符号数乘法指令的功能。

### 1. 加法和减法指令组

==加法和减法指令组含有如下8条指令：ADD、ADC、INC、SUB、SBB、DEC、CMP和NEG==。

除了这些指令的操作数可以扩展到32位外，其他均与8086/8088相同。例如：

```assembly
ADD 	EAX,ESI
ADC 	EAX,DWORD PTR [BX]
INC 	EBX
SUB 	ESI,4
SBB 	DWORD PTR [EDI],DX
DEC 	EDI
CMP 	EAX,EDX
NEG 	ECX
```

### 2. 乘法和除法指令组

==乘法和除法指令组含有4条指令：MUL、DIV、IMUL和IDIV。==

#### (1) 无符号数乘法和除法指令

==无符号数乘法和除法指令的格式没变。指令中只给出一个操作数，自动根据给出的操作数确定另一个操作数。==当指令中给出的源操作数为字节或字时，它们与8086/8088的情形相同。

==在源操作数为双字的情况下，乘法指令MUL默认的另一个操作数是EAX，其功能是把EAX内容乘上源操作数内容所得积送入`EDX:EAX`中，若结果的高32位（在EDX中）为0,那么标志CF和OF被清0,否则被置1;除法指令DIV默认的被除数是`EDX:EAX`，其功能是把指令中给出的操作数作为除数，所得的商送EAX,余数送EDX==。例如：

```assembly
MUL 	DWORD PTR [BX+3]
DIV 	EBX
```

#### (2) 有符号数乘法和除法指令

==原有的有符号数乘法指令IMUL和除法指令IDIV继续保持，但操作数可扩展到32位。当操作数为32位时的情形与无符号数乘法、除法指令相同==。

从80186开始，还提供了新形式的有符号数乘法指令。一般格式如下：

```assembly
IMUL 	DST,SRC
IMUL	DST,SRC1,SRC2
```

上述==第一种格式是将目的操作数DST与源操作数SRC相乘，结果送到目的操作数DST中==；==第二种格式是将SRC1与SRC2相乘，结果送目的操作数DST中==。

其中，==目的操作数只能是16位通用寄存器或32位通用寄存器==；==第一格式中的源操作数SRC的长度必须与目的操作数的长度相同（8位立即数除外）,可以是通用寄存器、存储单元或立即数==；==第二格式中的源操作数SRC1只能是通用寄存器或存储单元，并且长度必须与目的操作数的长度相同，源操作数SRC2只能是立即数==。例如：

```assembly
IMUL 	EAX,10
IMUL 	AX,BX,12
IMUL 	DX,[SI],-2
IMUL 	EAX,DWORD PTR [EBX+ESI*2+3456H],5
```

实际上，第一种格式是第二种格式的特殊情形。例如：

```assembly
IMUL 	AX,7
IMUL 	AX,AX,7
```

对于这两种新增的乘法指令，由于存放积的目的操作数的长度与被乘数（或乘数）的长度相同，因此积有可能溢出。==如果积溢出，那么高位部分将被丢掉，而置标志CF和OF为1来表示溢出；否则清标志CF和OF。所以，在这样的乘法指令后可安排检测OF的条件转移指令，用于处理积溢出的情况。==

由于存放积的目的操作数的长度与乘数的长度相同，而有符号数或无符号数的乘积的低位部分是相同的。所以，这种==新形式的乘法指令对无符号数和有符号数的处理是相同的==。这也是只用一条IMUL指令就可以代表新形式乘法指令的原因。

在80186和80286中，没有32位通用寄存器可使用，此外第一种格式中的源操作数只能是立即数。

### 3. 符号扩展指令组

==80386的符号扩展指令组有如下4条指令：CBW、CWD、CWDE和CDQ。==

符号扩展指令CBW和CWD的功能均无变化。

符号扩展指令CWDE和CDQ是80386新增的指令。它们的格式如下：

```assembly
CWDE
CDQ
```

==指令CWDE将16位寄存器AX的符号位扩展到32位寄存器EAX的高16位中。该指令是指令CBW的扩展。==

==指令CDQ将寄存器EAX的符号位扩展到EDX的所有位。该指令是指令CWD的扩展。==

==这些指令均不影响各标志。==

### 4.十进制调整指令组

==十进制调整指令组含有如下6条指令：DAA、DAS,AAA、AAS、AAM和AAD。这些指令的功能与8086/8088相同，均无变化。==

## 9.3.3 逻辑运算和移位指令

80386的逻辑运算和移位指令包括

- 逻辑运算指令
- 一般移位指令
- 循环移位指令
- 双精度移位指令

### 1. 逻辑运算指令组

逻辑运算指令组含有如下5条指令：NOT、AND、OR、XOR和TEST。

除了这些指令的操作数可以扩展到32位外，其他均与8086/8088相同。例如：

```assembly
NOT 	EAX 
AND 	EDX,OFFOOFFOH
XOR 	EAX,EAX
OR 		ESI,[BX]
TEST 	EAX,ES:[1234H]
```

### 2. 一般移位指令组

==一般移位指令组含有如下3条指令:SAL/SHL、SAR和SHR。算术左移指令SAL和逻辑左移指令SHL是相同的==

从80386开始，操作数可扩展到32位。尽管这些指令的格式没变，但移位位数的表达增强了。==从80186开始，移位指令中的移位位数不仅可以是1或者CL,也可以是一个8位立即数。==例如：

```assembly
SHL		AL,4
SHR 	EAX,12
SAR 	WORD PTR [SI],3
```

==从80386开始，实际移位位数等于指令中指定的移位位数的低5位，所以移位位数的变化范围是0至31==。

==CF总是保留着目的操作数最后被移出去的位的值==。但对==逻辑左移指令SHL而言，当移位位数大于等于被移位的操作数长度时，CF被置0==。

==在移位位数仅是1的情况下，当移位前后的目的操作数的符号位相同时，那么溢出标志位OF被置0==。

这些移位指令还会影响标志ZF,SF和PF,但对标志AF无定义。

如下的程序片段实现把DX的内容乘16再加上AX的内容，结果保存在`DL:AX`中，设它们为无符号数：

```assembly
PUSH 	DX
SHL 	DX,4
ADD 	AX,DX
POP		DX
PUSHF
SHR 	DX,12
POPF
ADC 	DL,0
```

如下的指令片段利用算术右移指令SAR实现除数为2的n(设为5)次方的除法，为了保证所得的商与利用IDIV指令后所得的商相同，在被除数是负数的情况下，先把负数加$2^n-1$:

```assembly
		OR 	EAX,EAX
		JGE NOADJ
		LEA EAX,[EAX+31]
NOADJ:
		SAR EAX,5
```

### 3. 循环移位指令组

==循环移位指令组含有如下4条指令：ROL、ROR、RCL和RCR。==

从80386开始，操作数可扩展到32位。与一般移位指令一样，从80186开始，循环移位指令中的移位位数也可以是一个8位的立即数。

例如：

```assembly
ROL 	AH,4
ROR 	ESI,12
RCL 	AX,3
RCR 	ECX,4
```

==从80386开始，对循环指令ROL和ROR而言，实际移位的位数将根据被移位的操作数的长度取8、16或32位的模==；==对带进位的循环移位指令RCL和RCR而言，移位位数先取指令中规定的移位位数的低5位，再根据被移位的操作数的长度取9、17或32位的模。==

==循环移位指令影响CF和OF。CF总是保留着操作数最后被移出去的位的值。在移位位数仅是1的情况下，当移位前后的目的操作数的符号位相同时，那么OF被置0==。

例：设要将寄存器AX的每一位依次重复一次，所得32位数保存在寄存器EAX中。如下程序片段实现这一要求：

```assembly
		MOV 	CX,16
		MOV 	BX,AX
NEXT:
		SHR 	AX,I
		RCR 	EDX,1
		SHR 	BX,1
		RCR 	EDX,1
		LOOP 	NEXT
		MOV 	EAX,EDX
```

### 4. 双精度移位指令组（始于80386)

==始于80386的双精度移位指令组含有两条指令：SHLD和SHRD。==双精度移位指令的一般格式如下：

```assembly
SHLD 	OPRD1,OPRD2,m
SHRD 	OPRD1,OPRD2,m
```

其中，==操作数OPRD1可以是16位通用寄存器、16位存储单元、32位通用寄存器或者32位存储单元==；==操作数OPRD2的长度必须与操作数OPRD1的长度一致，并且只能是16位通用寄存器或者32位通用寄存器==；==m是移位位数，或者为8位立即数，或者为CL。==

==双精度左移指令SHLD的功能是把操作数OPRD1左移指定的m位，空出的位用操作数OPRD2高端的m位填补==，(类似基于`OPRD1:OPRD2`的左移）,==但操作数OPRD2的内容不变，最后移出的位保留在进位标志CF中。如果只移一位，当进位标志和最后的符号位不一致时，置溢出标志OF,否则清OF。==例如：

```assembly
MOV 	AX,8321H
MOV 	DX,5678H
SHLD 	AX,DX,1 		;AX=0642H,DX=5678H,CF=1,OF=1
SHLD 	AX,DX,2 		;AX=1909H,DX=5678H,CF=0,OF=0
```

==双精度右移指令SHRD的功能是把操作数OPRD1右移指定的m位，空出的位用操作数OPRD2低端的m位填补==，(类似基于OPRD1:OPRD2的右移）,==但操作数OPRD2的内容不变，最后移出的位保留在进位标志CF中。当移位位数为1时，OF标志受影响。否则清OF。==例如：

```assembly
MOV 	EAX,01234867H
MOV 	EDX,5ABCDEF9H
SHRD 	EAX,EDX,4				;EAX=90123486H,CF=0,OF=1
SHRD 	EAX,EDX,8 				;EAX=F9901234H,CF=1,OF=0
```

==双精度移位指令还影响标志ZF、SF和PF==,对AF无定义。双精度移位指令的实际移位的位数取指令中规定的移位位数m的32位的低5位，所以移位位数的变化范围是0~31。如果移位的位数是0,那么双精度移位指令就相当于空操作指令NOP。如果移位位数超过被移位操作数OPRD1的长度，那么操作数OPRD1和各标志均无定义。

==如下一条指令可实现把EAX中的32位数，保存到寄存器对DX:AX中：==

```assembly
SHLD EDX,EAX,16
```

## 9.3.4 控制转移指令

控制转移指令可分为如下四组：

- 转移指令
- 循环指令
- 过程调用和返回指令
- 中断调用和中断返回指令

这些控制转移指令的非扩展形式的功能保持与8086/8088相同。由于保护方式下80386的段长可超过64K,所以，这些控制转移指令扩展后涉及的段内偏移可达32位。在采用32位表示段内偏移时，段间转移的目的地址采用48位全指针形式表示。但在实方式下，最大的段长仍是64K,因此，即使段内偏移用32位表示，实际的偏移值也禁止超过64K。

### 1. 转移指令组

#### (1) 无条件转移指令

==无条件转移指令JMP在分为段内直接、段内间接、段间直接和段间间接四类的同时，还具有扩展形式。扩展的无条件转移指令的转移目的地址偏移采用32位表示，段间转移目的地址采用48位全指针形式表示。==

在实方式下，无条件转移指令JMP的功能几乎没有提高。尽管80386的无条件转移指令允许把32位的段内偏移送到EIP,但在实方式下段最大64K,段内偏移不能超过64K,所以不需要使用32位的段内偏移。例如：

```assembly
JMP		EAX		;有效，但实方式下EAX不能超过64K
```

在保护方式下，段内无条件转移指令的转移方法未变，但段间无条件转移指令的执行细节较复杂，请参见10.6节。

#### (2) 条件转移指令

==80386的条件转移指令（除JCXZ和JECXZ指令外）允许用多字节来表示转移目的地偏移与当前偏移之间的差，所以转移范围可超出`-128~+127`==。例如：

```assembly
	JNZ	OK
	...			;超过127字节
OK:	...
```

这一点比8086/8088的条件转移指令的功能强，它使得程序员可不必考虑条件转移的范围。但在向前引用标号时，如果程序员能够预计到所引用的标号在127的范围之内，那么，在标号前加上汇编语言操作符SHORT可使汇编程序产生只有一字节地址差的条件转移指令。例如：

```assembly
	CMP 	EDX,ECX
	JBE 	SHORT OK
	XCHG 	EDX,ECX
OK:
```

==在80386中，当寄存器CX的值为0时，转移的指令JCXZ可以被扩展到JECXZ==，例如：

```assembly
JECXZ	OK
```

它表示当32位寄存器ECX为0时，转移到标号OK处。==但必须注意，指令JCXZ和JECXZ与其他条件转移指令不同，仍只能用一字节表示地址差值，所以转移范围仍是128~127==。

### 2. 循环指令组

==循环指令组含有如下三条指令：LOOP、LOOPZ/LOOPE和LOOPNZ/LOOPNE。==

这三条循环指令的非扩展形式保持原功能。它们的扩展形式使用ECX作为计数器，即从CX扩展到ECX。汇编程序TASM支持使用助记符LOOPD、LOOPZD/LOOPDE和LOOPDNZ/LOOPDNE,以便明确说明使用ECX作为计数器；同样，可使用助记符LOOPW、LOOPWZ/LOOPWE和LOOPWNZ/LOOPWNE,以便明确说明使用CX作为计数器。

这三条循环指令的转移范围仍是`-128~+127`。

如下过程SORT实现了“冒泡”法排序。

```assembly
;过程名称，SORT
;功能：将缓冲区中的双字有符号数按小到大排序
;人口参数：ESI=缓冲区首地址偏移，ECX=缓冲区中待排序数据的个数（≥2)
;出口参数：缓冲区中已排序好的数据
SORT	PROC
DEC		ECX
OUTLOOP:
		MOV 	EDX,0
INNERLOOP:
		CMP 	EDX,ECX
		JAE 	SHORT BOTTOM
		MOV 	EAX,[ESI+EDX*4+4]
		CMP 	[ESI+EDX*4],EAX
		JGE 	SHORT NOSWAP
		XCHG 	[ESI+EDX*4],EAX
		MOV		[ESI+EDX*4+4],EAX
NOSWAP:
		INC 	EDX
		JMP 	INNERLOOP
BOTTOM: 
		LOOP 	OUTLOOP
		RET
SORT	ENDP
```

### 3. 过程调用和返回指令组

过程调用指令CALL在分为段内直接、段内间接、段间直接和段间间接四类的同时，还具有扩展形式。扩展的调用指令的转移目的地址偏移采用32位表示。==对于扩展的段间调用指令，转移目的地址采用48位全指针形式表示，而且在把返回地址的CS压入堆栈时扩展成高16位为0的双字，这样会压入堆栈2个双字。==

过程返回指令RET在分为段内返回和段间返回的同时，还分别具有扩展形式。==扩展的过程返回指令要从堆栈弹出双字作为返回地址的偏移。如果是扩展的段间返回指令，执行时要从堆栈弹出包含48位返回地址全指针的2个双字。==

在实方式下主要使用过程调用指令CALL和过程返回指令RET的非扩展形式，它们与8086/8088的CALL指令和RET指令相同。==在保护方式下，段内过程调用指令和返回指令的转移方法未变，但段间过程调用和返指令的执行细节较复杂==，请参见10.6节。

下列过程FACT的功能是计算n!,它通过调用递归过程-FACT实现其功能。

```assembly
;过程名称：FACT
;功能：计算n!
;人口参数：EAX=n
;出口参数：EAX=n!
;说明：如果溢出，那么EAX=-1
FACT 	PROC
		PUSH 	BX
		PUSH 	ECX
		MOV 	BL,0			;BL=1表示滋出
		MOV 	ECX,EAX
		CMP 	ECX,0			;负数无意义
		JL 		SHORT FACT1
		CALL 	_FACT			;调用递归过程
		CMP		BL,1			;溢出？
		JNZ 	SHORT FACT2
FACT1:
		MOV		EAX,-1			;溢出处理
FACT2:
		POP		ECX
		POP		BX
		RET
FACT 	ENDP

;过程名称：_FACT
;人口参数：ECX=n,BL=0
;出口参数：EAX=n!,BL=1表示溢出
_FACT 	PROC
		CMP 	ECX,0
		JZ 		SHORT _FACT2
		PUSH 	ECX				;保存n
		DEC 	ECX
		CALL	_FACT			;计算（n-1)!
		POP		ECX				;n
		IMUL 	EAX,ECX			;in!=nX(n-1)!
		JNO 	SHORT _FACT1	;是否溢出？
		MOV 	BL,1			;是
_FACT1:
		RET
_FACT2:
		MOV 	EAX,1			;0!=1
		RET
_FACT 	ENDP
```

在4.5节已有一个计算n!的递归过程，请把这里的递归过程与之作比较。

### 4. 中断调用和中断返回指令组

==在实方式下，中断调用指令INT的功能与8086/8088的INT指令相同。==

==在保护方式下，中断调用指令INT把扩展的标志寄存器EFLAG、CS和EIP压入堆栈，也即压入堆栈3个双字，在压入CS时也扩展到32位，高16位为0。==具体执行细节较复杂，在10.7节中说明。

中断返回指令==IRET==有非扩展和扩展两种形式。在实方式下，总是使用其非扩展形式，其功能与8086/8088的IRET指令相同。==在保护方式下，应该使用其扩展形式，以与保护方式下的中断调用指令相对应。==具体执行细节较复杂，在10.7节中说明。

在实方式下，溢出中断调用指令INTO的功能与8086/8088相同。在保护方式下，该指令的执行细节较复杂，请参见10.7节。

## 9.3.5 串操作指令

从80386开始，串操作的基本单位在字节和字的基础上增加了双字。从80186开始在8086/8088的5条基本串操作指令的基础上，==增加了串输入操作指令INS和串输出操作指令OUTS==。

### 1. 基本串操作

指令对应于字节和字为元素的基本串操作指令没变化。对应于双字为元素的基本审操作指令格式如下：

```assembly
LODSD			;串装入指令
STOSD			;串存储指令
MOVSD			;申传送指令
SCANSD			;串扫描指令
CMPSD			;串比较指令
```

其中，==LODSD、STOSD和SCANSD指令使用累加器EAX;在DF=0时，每次执行串操作后相应指针加4,在DF=1时，每次串操作后相应指针减4==。

这些以双字为元素的基本串操作指令的功能和使用方法与以字节或字为元素的基本串操作指令一样。它们分别是对应以字为元素的串操作指令的扩展。

==在不使用32位指针的情况下，串操作中的源指针是`DS:SI`,目的指针是`ES:DI`;在使用32位指针（地址扩展）的情况下，源指针是`DS:ESI`,目的指针是`ES:EDI`。此外，可以通过段前级超越的方法改变源串采用的段寄存器，但不能改变目的串的段寄存器。==在实方式下，通常使用16位指针。

下面介绍一个简单的位串传送过程SBITBLT。该过程能够把长度为32的倍数的位串传送到指定存储单元开始的缓冲区中，源串可以从双字中的任意位开始，但目标串必须对齐字节的边界。图9.4是源位串和目标位串的示意图。

```assembly
;过程名称：SBITBLT
;功能：简单位串传送
;人口参数：DS:ESI=源位串开始单元地址偏移
;		ES:EDI=目标申开始单元地址偏移
;		EBX=要传送的位串长度（以双字为单位）
;		ECX=要传送的位串在源串的第一个双字中的位偏移量
;出口参数：无
;说明：(1)位申长度必须是双字的倍数
;	(2)目标位串必须从目标单元的第一个字节的边界处开始
SBITBLT 	PROC
			CLD
			MOV 	EDX,DWORD PTR [ESI]
			ADD 	ESI,4
BITLOOP:
			LODSD
			SHRD 	EDX,EAX,CL
			XCHG 	EAX,EDX
			STOSD
			DEC		EBX
			JNZ 	BITLOOP
			RET
SBITBLT 	ENDP
```

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.3dwr5bgq7fi0.webp)

### 2. 重复前缀

==重复前缀REP、REPZ/REPE和REPNZ/REPNE,在仍采用16位地址偏移指针的情况下以CX作为重复计数器，在采用32位地址偏移的扩展情况下以ECX作为重复计数器。==由于在实方式下通常采用16位指针，所以一般仍以CX作为计数器。

例如：在80386实方式下执行的如下程序片段将把长度为（CX)字节的数据块从由`DS:SI`所指向的源区传到由`ES:DI`所指向的目的区：

```assembly
ROR 	ECX,2
REP 	MOVSD			;使用CX为计数器，每次传双字
ROL 	ECX,1
REP 	MOVSW			;传可能余下的字
ROL 	ECX,1
REP 	MOVSB			;传可能余下的字节
```



### 3. 串输人指令串

输入指令的格式如下：

```assembly
INSB	;输入字节（Byte)
INSW	;输入字（Word)
INSD	;输入双字（Dword),始于80386
```

==串输入指令从由DX给出端口地址的端口读入一字符，并送入由`ES:DI`(或EDI)所指的目的串中，同时根据方向标志DF和字符类型调整DI(或EDI)==。

==INSB指令对应的字符类型是字节，INSW指令对应的字符类型是字，INSD指令对应的字符类型是双字。根据输入字符的上述类型，当DF=0时，对目的指针的调整值依分别是1、2或4,当DF=1时，对目的指针的调整值依次分别是-1、-2或-4==。

在汇编语言中，==三条串输入指令的格式可统一为如下一种格式：==

```assembly
INS DSTS,DX
```

==汇编程序根据目的串DSTS类型决定使用字节输入指令、字输入指令或双字输入指令==。也即，如果类型为字节，则采用INSB指令；如果类型为字，则采用INSW指令；如果类型是双字，则采用INSD指令。请注意，目的串DSTS并不影响实际使用指针`ES:DI`(或EDI)及其值，所以在使用上述格式的串输入指令时，仍必须先给`ES:DI`(或EDI)赋合适的值。

==串输入指令不影响标志==

在串输入指令前，可使用重复前缀REP,以便连续输入，但必须注意端口的数据准备情况。

### 4. 串输出指令

串输出指令的格式如下

```assembly
OUTSB		;输出字节（Byte)
OUTSW		;输出字（Word)
OUTSD		;输出双字（Dword),始于80386
```

串输出指令把由`DS:SI`(或ESI)所指的源串中的一个字符，输出到由DX给出的端口，同时根据方向标志DF和字符类型调整SI(或ESI)。

==OUTSB指令对应的字符类型是字节，OUTSW指令对应的字符类型是字，OUTSI指令对应的字符类型是双字。根据输入字符的上述类型，当DF=0时，对源指针的调整值依次分别是1、2或4:当DF=1时，对源指针的调整值依次分别是-1、-2或-4。==

在汇编语言中，三条串输出指令的格式可统一为如下一种格式：

```assembly
OUTS DX,SRCS
```

==汇编程序根据源串SRCS类型决定使用字节输出指令、字输出指令或双字输出指令==。也即，如果类型为字节，则采用OUTSB指令；如果类型为字，则采用OUTSW指令；如果类型是双字，则采用OUTSD指令。请注意，指令中给出的源串并不影响实际使用指针`DS:SI`(或ESI)及其值，所以在使用上述格式的串输出指令时，仍必须先给`DS:SI`(或ESI)赋合适的值。

==串输出指令不影响标志==。

在串输出指令前，可使用重复前缀REP,以便连续输出，但必须注意端口的数据接收处理情况。

## 9.3.6 高级语言支持

指令高级语言支持指令始于80186,它们用于简化支持高级语言的某些特征。共有3条这样的指令，它们是：BOUND、ENTER和LEAVE

### 1. 建立与释放堆栈框架指令

在C和PASCAL等高级语言中，函数或过程不仅通过堆栈传递入口参数，而且它们的局部变量也被安排在堆栈中，为了方便地获取入口参数和准确地存取局部变量，就要建立合适的堆栈框架。

对照比较如下的C函数和对应的汇编语言过程，就可看到堆栈框架的建立和使用情况。

为了说明情况而设计的一个简单的C函数sum如下：

```c
sum(int x, int y)	/*求两数之和函数*/
{					/*x和y是入口参数*/
    int sum;		/*定义一个局部变量*/
    sum=x+y;		/*求和*/
    return(sum);	/*返回*/
}
```

对应上面函数sum的汇编语言过程如下：

```assembly
_sum 	proc 	near
		push 	bp
		mov 	bp,sp				;建立堆找框架
		sub 	sp,2				;堆栈框架如图9.5所示
		;
		mov 	ax,word ptr[bp+4]	;取参数x
		add 	ax,word ptr[bp+6]	;加参数y
		mov 	word ptr [bp-2],ax 	;保存x+y之和到局部变量 sum
		;
		mov 	ax,word ptr[bp-2]	;取返回参数
		;
		mov 	sp,bp				;释放堆栈框架
		pop 	bp
		ret							;返回
_sum 	endp
```

如果利用始于80186的建立和释放堆栈框架指令ENTER和LEAVE,那么上面的汇编语言过程可优化如下：

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.5j15657mnl00.webp)

```assembly
_sum 	proc 	near
		enter 	2,0					;建立堆栈框架
		;
		mov		ax,word ptr[bp+4]
		add 	ax,word ptr[bp+6]
		mov 	word ptr [bp-2],ax
		mov		ax,word ptr[bp-2]
		;
		leave						;释放堆栈框架
		ret
_sum 	endp
```

#### (1) 建立堆栈框架指令ENTER

> ENTER CNT1,CNT2
>
> CNT1：局部参数个数
>
> CNT2：形参个数

建立堆栈框架指令ENTER的一般格式如下：

```assembly
ENTER CNT1,CNT2
```

其中，==操作数CNT1是16位立即数，表示框架的大小，也即子程序需要安排在堆栈中的局部变量所需的字节数；操作数CNT2是8位立即数，表示子程序嵌套级别，也即需要从外层框架复制到当前框架的指针数==。

ENTER指令有非扩展和扩展两种形式。汇编程序TASM支持使用助记符ENTERW和ENTERD,以便明确说明使用该指令的非扩展形式和扩展形式。在实方式下，通常使用非扩展形式。

操作数CNT2为0时，非扩展形式的ENTER指令建立堆栈框架时所完成的操作如下：

1. BP进栈，即保存原堆栈框架指针
2. `BP <- SP`
3. `SP <- SP - 16位立即数（CNT1)`

例：如下指令建立BP指示的堆栈框架，堆栈顶有4字节的局部变量：

```assembly
ENTER 4,0
```

在80386的实方式下，使用该指令的扩展形式，也可建立由EBP指示的堆栈框架。上述操作步骤改变为：

1. EBP进栈，即保存原堆栈框架指针
2. `EBP <- ESP`
3. `SP <- SP-16位立即数（CNT1)`

只调整SP的原因是实方式下不使用ESP作为堆栈指针。

==在80386的保护方式下使用该指令的扩展形式，那么BP和SP寄存器分别扩展为EBP和ESP。==

==ENTER指令不影响标志==。

#### (2) 释放堆栈框架指令LEAVE

释放堆栈框架指令LEAVE的一般格式如下：

```assembly
LEAVE
```

==指令LEAVE的功能与指令ENTER相反，释放当前子程序（过程）在堆栈中的局部变量，使BP和SP恢复成最近一次的ENTER指令被执行前的值，==具体操作如下：

1. `SP <- BP`
2. `BP退栈`

LEAVE指令也有非扩展和扩展两种形式。汇编程序TASM支持使用助记符LEAVEW和LEAVED,以便明确说明使用该指令的非扩展形式和扩展形式。

在80386的实方式下，使用该指令的扩展形式，那么上述操作步骤改变为：

1. `SP <- BP`
2. `EBP退栈`

==在80386的保护方式下使用该指令的扩展形式，那么BP和SP寄存器分别扩展为EBP和ESP。==

==LEAVE指令不影响标志==。

注意，==指令LEAVE只负责释放堆栈框架，不实现返回，所以在过程中LEAVE指令后还应安排相应的RET指令==

下面的过程利用指令ENTER和LEAVE建立和释放堆栈框架。该过程利用选代算法计算n!,出入口参数与在9.3.4节中介绍的过程FACT相同，请作比较。

```assembly
ERROR 	EQU 	SS:BYTE PTR [BP-1]
;
FACT 	PROC
		ENTER		4,0			;建立堆栈框架
		MOV 		ERROR,0
		CMP 		EAX,0
		JL 			FACT4
		PUSH 		ECX
		MOV 		ECX,EAX
		MOV 		EAX,1
		JE 			FACT3
FACT1:
		IMUL 		EAX,ECX
		JNO 		FACT2
		MOV 		ERROR,1
FACT2:
		LOOPD 		FACT1		;使用ECX作为计数器
FACT3:
		POP			ECX
		CMP 		ERROR,1
		JZ 			FACT4
		LEAVE					;释放堆栈框架
		RET
FACT4: 
		MOV 		EAX,-1
		LEAVE					;释放堆栈框架
		RET
FACT 	ENDP
```

### 2. 检查数组下标界限指令BOUND

> 检查数组下界？

检查数组下标界限指令BOUND的一般格式如下：

```assembly
BOUND	OPRD1,OPRD2
```

在80386之前，用于给出待检查数组下标的操作数OPRD1是16位寄存器，用于给出数组下标上下界限的操作数OPRD2是32位存储器操作数，其中低字含起始下标，高字含结尾下标；从80386开始，OPRD1还可以是32位寄存器，此时OPRD2只能是64位存储器操作数，其中低双字含起始下标，高双字含结尾下标。

==该指令检查由OPRD1给出的有符号数是否在由操作数OPRD2给出的数组界限之内。如果被检查的下标不在数组允许的范围之内，那么产生类型号为5的异常（中断）==。

指令BOUND不影响标志。例如：

```assembly
NUM	=	100
ARRAY 	DB 	NUM DUP (0)
STV 	DW  0
EDV 	DW  NUM - 1
		...
		BOUND 	SI,DWORD PTR STV		;SI含下标
		MOV 	AL,ARRAY[SI]
		...
```

## 9.3.7 条件字节设置指令

从80386开始新增加了一组条件字节设置指令。这些指令根据一些标志位设置某个字节的内容为1或0。

条件字节设置指令的一般格式如下：

```assembly
SETcc OPRD
```

==其中，cc是指令助记符的一部分，用于表示条件，这些条件与条件转移指令中的条件相同，操作数OPRD只能是8位寄存器或者存储单元，用于存放测试的结果==。

==这些指令的功能是测试指令中规定的条件，若条件为`真`，那么将目的操作数OPRD置成1,否则置成0。==

![image](https://github.com/YangLuchao/img_host/raw/master/root/image.stmopwj44xs.webp)

==条件字节设置指令与条件转移指令一样，并且测试条件的方法也相同，只是在条件满足时，设置某个字节而已。==这些条件字节设置指令共有16条，列于表9.2,为了便于记忆和使用，有些指令有多个助记符。

==这些条件字节设置指令也可像条件转移指令那样分为三类。==

这些条件字节设置指令不影响各标志。

例：如下程序片段测试含于寄存器EAX中的八位16进制数是否有一位为0(BH=0表示没有一位是0):

```assembly
		MOV 	BH,0
		MOV 	CX,8
NEXT:
		TEST	AL,OFH
		SETZ 	BL
		OR 		BH,BL
		ROR 	EAX,4
		LOOP 	NEXT
```

例：如下程序片段统计DS:SI所指向的单字节数据缓冲区中正数和负数的个数（设缓冲区以0结尾）:

```assembly
		CLD
		XOR		DX,DX			;计数器清0
NEXT:
		LODSB	
		CMP 	AL,0
		JZ 		SHORT OVER
		SETG 	BL				;正数时BL=1,否则BL=0
		SETL 	BH				;负数时BH=1,否则BH=0
		ADD 	DL,BL
		ADD 	DH,BH
		JMP 	NEXT
OVER:
```

## 9.3.8 位操作指令

==从80386开始增加了位操作指令。这些位操作指令可以直接对一个二进制位进行测试、设置和扫描等操作==。利用这些指令可以更有效地进行位操作。

==位操作指令可分为位扫描指令组和位测试及设置指令组==。

### 1. 位测试及设置指令组

位测试和设置指令组含有如下4条指令

- 位测试（Bit Test)指令BT
- 位测试并取反(Bit Test and Complement)指令BTC
- 位测试并复位（Bit Test and Reset)指令BTR
- 位测试并置位（Bit Test and Set)指令BTS

这4条位测试和设置指令的格式如下：

```assembly
BT 	OPRD1,OPRD2
BTC OPRD1,OPRD2
BTR OPRD1,OPRD2
BTS OPRD1,OPRD2
```

其中，==操作数OPRD1可以是16位或32位通用寄存器和16位或32位存储单元，用于指定要测试的内容==；

==操作数OPRD2必须是8位立即数或者与操作数OPRD1长度相等的通用寄存器，用于指定要测试的位==。

==设操作数OPRD2除以操作数OPRD1的长度后所得商是disp，所得余数是offset。==用么这些指令要测试的位是根据如下方法确定的：==如果操作数OPRD1是寄存器，offset是寄存器操作数OPRD1中要测试位的位号；如果操作数OPRD1是存储单元，存储器操作数OPRD1的偏移与disp相加之和是实际测试存储单元的偏移，offset是该存储单元中要测试位的位号。==操作数OPRD2取符号整数值，所以当OPRD2为16位时，可访问（-32K)至（32K-1)范围内的位串，当OPRD2是32位时，可访问（-2G)至（2G-1)范围内的位串。

==位测试指令BT的功能是把被测试位的值送标志位CF==。

==位测试并取反指令BTC的功能是把被测试位的值送标志CF，并且把被测试位取反==

==位测试并复位指令BTR的功能是把被测试位的值送标志CF，并且把被测试位复位，也即清0==

==位测试并置位指令BTS的功能是把被测试位的值送标志CF，并且把被测试位置位，也即置1==

其它标志CF、SF、OF、AF和PF无定义。

例如：

```assembly
MOV 	BX,4567H
MOV 	ECX,3
BT 		BX,CX			;CF=0,BX=4567H
BTC 	BX,3			;CF=0,BX=456FH
BTR 	BX,CX			;CF=1,BX=4567H
BTS 	EBX,ECX			;CF=0,BX=456FH
```

假设数据段有如下变量：

```assembly
IMAGEW 	DW	1234H,5678H
IMAGED	DD	12345678H
```

代码段有如下指令（设已置妥DS):

```assembly
BT		IMAGEW,4			;CF=1,[IMAGEW]=1234H
MOV		CX,22
BTC		IMAGEW,CX			;CF=1,[IMAGEW+2]=5638H
BTR		IMAGED,6			;CF=1,[IMAGED]=12345638H
MOVZX	EAX,CX
BTS		IMAGED,EAX			;CF=0,[IMAGED]=12745678H
```

要特别指出，==在这些位测试指令中，如果用于指定测试位号的操作数OPRD2是立即数，那么其值不应超过被测试操作数OPRD1的长度，否则将产生未定义的位偏移量。==这个规则允许规定在一个寄存器内的任何位移量，而且将存储器位串中的立即数位移量限制在规定存储单元字或双字之内。但汇编程序可以支持对于内存位串的更大的立即数位偏移量，汇编程序可将该立即数位移量的低5位（对于32位操作数）或低4位（对于16位操作数）作为机器指令中的操作数OPRD2,将该立即数位移量的相应高位右移后加到内存位串开始单元的偏移上，作为机器指令中的操作数OPRD1。

例：如下程序片段把寄存器AL的位0、2、4、6依次重复一次，所得的8位数保存在寄存器AL中：

```assembly
		MOV 	DL,0
		MOV 	CX,4
		MOV 	BX,0
NEXT:
		BT		AX,BX			;依次测0、2、4、6位
		SETC 	AH				;根据被测位值，置AH
		OR 		DL,AH
		ROR 	DL,1
		OR 		DL,AH
		ROR 	DL,1
		INC 	BX
		INC 	BX
		LOOP 	NEXT
		MOV 	AL,DL
```

### 2. 位扫描指令组

位扫描指令组含有如下2条指令：

==顺向位扫描（Bit Scan Forward)指令BSF==

==逆向位扫描（Bit Scan Reverse)指令BSR==

这两条位扫描指令的格式如下：

```assembly
BSF 	OPRD1,OPRD2
BSR 	OPRD1,OPRD2
```

其中，==操作数OPRD1和OPRD2可以是16或32位通用寄存器和16位或32位存储单元；但操作数OPRD1和OPRD2的位数（长度）必须相等==。

==顺向位扫描指令BSF的功能是从右向左（位0~位15或位31)扫描字或者双字操作数OPRD2中第一个含`1`的位，并把扫描到的第一个含`1`的位的位号送操作数OPRD1==

==逆向位扫描指令BSR的功能是从左向右（位15或位31~位0)扫描字或者双字操作数OPRD2中第一个含`1`的位，并把扫描到的第一个含`1`的位的位号送操作数OPRD1==

==如果字或双字操作数OPRD2等于0,那么零标志ZF被置1,操作数OPRD1的值不确定；否则零标志ZF被清0。==

其它标志CF、SF、OF、AF和PF无定义。例如：

```assembly
MOV 	EBX,12345678H
BSR 	EAX,EBX			;EAX=1CH,ZF=0
BSF 	DX,AX			;DX=2.ZF=0
BSF 	CX,DX			;CX=1,ZF=0
```

例：如下程序片段处理AX中的16位图形信息，仅保留可能有的最右和最左各一位为`1`的位：

```assembly
		XOR 	DX,DX
		BSF 	CX,AX
		JZ 		SHORT DONE
		BTS 	DX,CX
		BSR 	CX,AX
		JZ 		SHORT DONE
		BTS 	DX,CX
DONE:
		MOV		AX,DX
```

## 9.3.9 处理器控制指令

处理器控制指令用于设置标志、空操作和与外部事件同步等。这里介绍的处理器控制指令都始于8086/8088。

### 1. 设置标志指令组

==设置进位标志CF的指令CLC、STC和CMC保持与原先相同==。

==设置方向标志DF的指令CLD和STD保持与原先相同==。

设置中断允许标志IF的指令CLI和STI的功能在实方式下保持与原先相同。在保护方式下它们是I/O敏感指令，请参见10.9节。

### 2. 空操作指令

空操作指令NOP的一般格式如下：

```assembly
NOP
```

==空操作指令的功能是什么都不做。该指令就一个字节的操作码。利用该指令可“填补程序中的空白区”，使代码保持连续==。

### 3. 外同步指令和前缀

#### (1) 等待指令WAIT

等待指令WAIT的一般格式如下：

```assembly
WAIT
```

==该指令的功能是等待直到BUSY引脚为高。BUSY由数值协处理器控制，所以该指令的功能是等待数值协处理器，以便与它同步==。该指令也能够检查数值协处理器是否故障，请参见10.7.2节。

#### (2) 封锁前级LOCK

==封锁前级LOCK可以锁定其后指令的目的操作数确定的存储单元，这是通过使LOCK信号在指令执行期间一直保持有效而实现的。在多处理器环境中，使用这种方法可以保证指令执行时独占共享内存==。

只有下列指令才能用封锁前缀LOCK,并且目的操作数是存储器操作数：

```assembly
XCHG
ADD
ADC
INC
SUB
SBB
DEC
NEG
OR
AND
XOR
NOT
BT
BTS
BTR
BTC
```

例如：

```assembly
LOCK BTC 	[EBX],EAX 
LOCK XCHG 	[BX],AX
```

# 9.4 实方式下的程序设计

在实方式下，80386相当于一个可进行32位处理的快速8086。为80386编写的程序可利用32位的通用寄存器，可使用新增的指令，可采用扩展的寻址方式。段的最大长度是64K,但不像真正的8086,当所存取的存储单元的地址偏移超过OFFFFH时，不会引起模64K的地址反绕，而是导致段越界异常。所以，在实方式下运行的程序访问的存储单元的地址偏移不能超过OFFFFH,转移的目的地址偏移也不能超过OFFFFH。

## 9.4.1 说明

### 1. 说明处理器类型的伪指令

在缺省情况下，MASM和TASM仅识别8086/8088的指令。==为了让MASM或TASM识别由80186、80286和80386等新增的指令或功能增强的指令，须告诉汇编程序处理器的类型。==汇编语言提供了如下说明处理器类型的伪指令，这些伪指令均以句点引导

```assembly
.8086			;只支持对8086指令的汇编
.186			;支持对80186指令的汇编
.286			;支持对非特权80286指令的汇编
.286C			;支持对非特权80286指令的汇编
.286P			;支持对80286所有指令的汇编
.386			;支持对80386非特权指令的汇编
.386C			;支持对80386非特权指令的汇编
.386P			;支持对80386所有指令的汇编
```

汇编程序在遇到说明处理器类型的伪指令后，就识别并汇编相应的指令。在一个源程序中，可根据需要安排多条说明处理器类型的伪指令，以便更改对处理器类型的说明。对TASM而言，这些伪指令可以安排在源程序中的任何位置。但对MASM而言，上述说明处理器类型的伪指令必须安排在段外

==只有在使用说明处理器类型是80386的伪指令后，汇编程序才识别表示32位寄存器的符号和表示始于80386的指令的助记符==。

如果执行目标程序的CPU是8088或8086，那么就不能指示汇编程序按其他处理器类型（80186、80286和80386等）来汇编源程序；类似地，如果执行目标程序的CPU是80286,那么就不能指示汇编程序按80386处理器来汇编源程序。

### 2. 关于段属性类型的说明

在实方式下，80386保持与原先的8086/8088兼容，所以段的最大长度仍是64K,这样的段称为16位段。但在保护方式下，段长度可达4G,这样的段称为32位段。为了兼容，在保护方式下，也可使用16位段。

在8.1.1节中介绍了段的定义，段定义语句带有可选的定位类型、组合类型和类别。此外，还有可选的段属性类型说明，用于指示是16位段，还是32位段。完整段定义的一般格式如下：

```assembly
段名 SEGMENT [定位类型] [组合类型] ['类别'] [属性类型]
```

==属性类型说明符号是`USE16`和`USE32`。`USE16`表示16位段，`USE32`表示32位段。在使用`.386`等伪指令指示处理器类型80386后，缺省的属性类型是`USE32`;如果没有指示处理器类型80386,那么缺省的属性类型是USE16。==

例：如下语句说明一个32位段：

```assembly
CSEG 	SEGMENT PARA USE32
		...
		...
CSEG ENDS
```

例：如下语句说明一个16位段：

```assembly
CSEG 	SEGMEN PARA USE16
		...
		...
		...
CSEG 	ENDS
```

注意，在实方式下运行的程序，只能使用16位段；此外，总是使用16位段的堆栈，也总是使用SP作为堆栈指针。

### 3. 操作数和地址长度前缀

尽管在实方式下只能使用16位段，但可使用32位操作数，也可使用以32位形式表示的存储单元地址，这是利用操作数长度前缀66H和存储器地址长度前级67H来表示的。

在16位代码段中，正常操作数的长度是16位或8位。在指令前加上操作数长度前缀66H后，操作数长度就成为32位或8位，也即原来表示16位操作数的代码成为表示32位操作数的代码。一般情况下，不在源程序中直接使用操作数长度前缀，而是直接使用32位操作数，操作数长度前缀由汇编程序在汇编时自动加上。

请注意比较如下在16位代码段中的汇编格式指令和对应的机器码（注释部分）:

```assembly
.386
TEST16 	SEGMENT PARA USE16
		......
		MOV 	EAX,EBX		;66H,8BH,C3H
		MOV		AX,BX		;8BH,C3H
		MOV 	AL,BL 		;8AH,C3H
		......
TEST16 	END
```

S32位代码段的情况刚好相反。在32位代码段中，正常操作数长度是32位或8位。在指令前加上操作数长度前缀66H后，操作数长度就成为16位或8位。不在32位代码的源程序中直接使用操作数长度前级66H表示使用16位操作数，而是直接使用16位操作数，操作数长度前级由汇编程序在汇编时自动加上。

请注意比较如下在32位代码段中的汇编格式指令和对应的机器码（注释部分）:

```assembly
.386
TEST32 	SEGMENT PARA USE32
		MOV 	EAX,EBX			;8BH,C3H
		MOV 	AX,BX			;66H,8BH,C3H
		......
TEST32 	ENDS
```

通过存储器地址长度前缀67H区分32位存储器地址和16位存储器地址的方法与上述通过操作数长度前级66H区分32位操作数和16位操作数的方法类似。在源程序中可根据需要使用32位地址，或者16位地址。汇编程序在汇编源程序时，对于16位的代码段，在使用32位存储器地址的指令前加上前级67H;对于32为代码段，在使用16位存储器地址的指令前加上前缀67H

在一条指令前可能既有操作数长度前级66H,又有存储器地址长度前缀67H。

## 9.4.2 实例

下面举两个例子，介绍如何编写在80386实方式下运行的程序，侧重于80386指令的应用，而不是算法的优化。

#### 例1: 写一个程序，以十进制、十六进制和二进制数三种形式显示双字存储单元F000:1234H的内容。

如下程序实现上述功能，并且在以三种形式显示时，都滤去了前导的0。

```assembly
;程序名：T9-1.ASM
;功能：(略）
		.386					;支持对80386非特权指令的汇编
CSEG 	SEGMENT USE16			;16位段
		ASSUME CS:CSEG
BEGIN: 
		MOV 	AX,0F000H
		MOV 	FS,AX
		MOV 	EAX,FS:[1234H]		;以十进制数形式显示
		CALL 	TODEC
		CALL 	NEWLINE				;以十六进制数形式显示
		CALL 	TOHEX
		MOV 	AL,'H'
		CALL 	ECHO
		CALL 	NEWLINE				;以二进制数形式显示
		MOV 	EAX,FS:[1234H]
		CALL 	TOBIN
		MOV 	AL,'B'
		CALL	ECHO
		CALL 	NEWLINE				;结束
		MOV		AH,4CH
		INT		21H					

;子程序名：TODEC
;功能：以十进制数形式显示32位值
;入口参数：EAX=要显示的值
;出口参数：无
TODEC 	PROC 	NEAR
		PUSHAD
		MOV		EBX,10
		XOR		CX,CX
DEC1: 
		XOR 	EDX, EDX
		DIV		EBX
		PUSH	DX
		INC		CX
		OR		EAX,EAX
		JNZ		DEC1
DEC2:
		POP		AX
		CALL	TOASC
		CALL	ECHO
		LOOP	DEC2
		POPAD
		RET
TODEC	ENDP
;子程序名：TOBIN
;功能：以二进制数形式显示32位值
;入口参数：EAX=要显示的值
;出口参数：无
TOBIN 	PROC 	NEAR
		PUSH 	EAX
		PUSH 	ECX
		PUSH 	EDX
		BSR		EDX,EAX
		JNZ 	BIN1
		XOR		DX,DX
BIN1:
		MOV		CL,31
		SUB		CL,DL
		SHL 	EAX,CL
		MOV 	CX,DX
		INC		CX
		MOV 	EDX,EAX
BIN2:
		ROL		EDX,1
		MOV 	AL,'0'
		ADC		AL,0
		CALL 	ECHO
		LOOP 	BIN2
		POP		EDX
		POP		ECX
		POP		EAX
		RET
TOBIN 	ENDP

;子程序名：TOHEX
;功能：以十六进制数形式显示32位值
;入口参数：EAX=要显示的值
;出口参数：无
TOHEX 	PROC 	NEAR
		COUNTB	=	8
		ENTER 	COUNTB,0
		MOVZX 	EBP,BP
		MOVE	CX,COUNTB
		MOV		EDX,EAX
HEX1:
		MOV		AL,DL
		AND		AL,0FH
		MOV		[EBP-COUNTB+ECX-1],AL
		ROR		EDX,4
		LOOP	HEX1
		MOV 	CX,COUNTB
		XOR		EBX,EBX
HEX2:
		CMP		BYTE PTR [EBP-COUNTB+EBX],0
		JNZ		HEX3
		INC 	EBX
		LOOP	HEX2
		DEC		EBX
		MOV		CX,1
HEX3:
		MOV		AL,[EBP-COUNTB+EBX]
		INC		EBX
		CALL 	TOASC
		CALL 	ECHO
		LOOP 	HEX3
		LEAVE
		RET
TOHEX 	ENDP
;子程序名：TOASC
;功能：把一位十六进制数转换为对应的ASCII码
;入口参数：AL=要显示的值
;出口参数：AL=ASCII码
TOASC 	PROC 	NEAR
		AND 	AL,0FH
		ADD		AL,'0'
		CMP		AL,'9'
		SETA 	DL
		MOVZX 	DX,DL
		IMUL 	DX,7
		ADD		AL,DL
TOASC1:
		RET
TOASC 	ENDP
;回车换行子程序
NEWLINE	PROC	NEAR
;内容略
NEWLINE ENDP
;显示字符子程序
ECHO 	PROC	NEAR
;内容略
ECHO 	ENDP
CSEG 	ENDS
		END		BEGIN
```

#### 例2:写一个程序，把以十进制数、十六进制数或二进制数形式输入的两个无符号整数相乘，用十进制数显示乘积。

如下程序实现上述功能，输入的两个整数最大以32位表示，超过认为溢出。当输入的数无效时，显示符号`x`，允许重新输入。十六进制数以符号H结尾，二进制数以符号B结尾，十进制数也可以符号D结尾。

```assembly
;程序名：T9-2.ASM
;功能：(略）
.386			;支持对80386非特权指令的汇编
CSEG 	SEGMENT USE16		;16位段
		ASSUME 	CS:CSEG
;子程序名：LTODEC
;功能：以十进制数形式显示一个64位无符号整数
;入口参数：EDX:EAX=64位二进制数
;出口参数：无
LTODEC 	PROC 	NEAR
		XOR 	CX,CX
LTDECL: 
		MOV 	EBX,10
		CALL	DIVX
		PUSH 	BX
		INC 	CX
		MOV 	EBX,ED
		XOR 	EBX,EAX
		JNZ 	LTDEC1
LTDEC2: 
		POP 	AX
		AND		AL,0FH
		ADD 	AL,'0'
		CALL 	ECHO
		LOOP 	LTDEC2
		RET
LTODEC 	ENDP
;子程序名：DIVX
;功能：64位数除以32位数，商用64位表示
;入口参数：EDX:EAX=被除数EBX=除数
;出口参数：EDX,EAX=商EBX=余数
DIVX 	PROC 	NEAR
		PUSH 	ECX
		PUSH 	ESI
		MOV 	CX,64
		XOR		ESI,ESI
DIVX1:
		SHL		EAX,1
		RCL 	EDX,1
		RCL 	ESI,1
		JC 		SHORT DIVX2
		CMP 	ESI,EBX
		JB 		SHORT DIVX3
DIVX2:
		SUB		ESI,EBX
		BTS 	AX,0
DIVX3:
		LOOP	DIVX1
		MOV 	EBX,ESI
		POP 	ESI
		POP 	ECX
		RET
DIVX 	ENDP
;子程序名：GETVAL
;功能：接收一个最大为32表示的无符号整数（可用三种进制表示）
;入口参数：无
;出口参数：EAX=接收到的无符号整数
;		BL=	0表示没有接收到数
;			1表示接收到的数符合要求
;			-1表示接收到的数无效
GETVAL 	PROC 	NEAR	
COUNTL = 36
		ENTER 	COUNTL,0
		PUSH 	ECX
		PUSH 	EDX
		PUSH 	DS
		PUSH 	ES
		;置段寄存器
		MOV 	AX,SS
		MOV 	DS,AX
		MOV 	ES,AX
		;接收一个字符串
		LEA 	EDX,[BP-COUNTL]
		MOV 	BYTE PTR [EDX],COUNTL-2
		MOV		AH,10
		INT		21H
		CALL 	NEWLINE
		;取字符申长度
		INC 	EDX
		MOV 	CL,[EDX]
		XOR		CH,CH
		MOVZX 	ECX,CX
		;去掉前导空格
		INC 	ECX
GVAL1:
		DEC		ECX
		INC 	EDX
		CMP 	BYTE PTR [EDX],' '
		JZ 		GVALI
		MOV 	BL,0
		JECXZ 	GVAL5
		;输入的字符串
		;空去掉尾部空格
GVAL2: 
		CMP 	BYTE PTR [EDX+ECX-1],''
		LOOPZ 	GVAL2
		SETNZ	AL
		ADD 	CL,AL
		JECXZ 	GVAL5				;处理可能的十进制数串
		MOV 	BL,-1
		MOV 	AL,[EDX+ECX-1]
		CMP 	AL,'0'
		JB 		SHORT GVAL5
		CMP 	AL,'9'
		JA 		SHORT GVAL3
GVAL2A:
		CALL	DSTOV
		JMP 	SHORT GVAL5
GVAL3:
		BTR		AX,5
		CMP 	AL,'D'
		JNZ		GVAL3A
		DEC 	ECX
		JMP		GVAL2A
		;处理可能的十六进制数串
GVAL3A: 
		CMP 	AL,'H'
		JNZ		GVAL4
		DEC 	ECX
		CALL 	HSTOV
		JMP 	SHORT GVAL5			;处理可能的二进制数串
GVAL4: 
		CMP 	AL,'B'
		JNZ 	SHORT GVAL5
		DEC 	ECX
		CALL 	BSTOV
GVAL5:
		POP		ES
		POP		DS
		POP		EDX
		POP		ECX
		LEAVE
		RET
GETVAL 	ENDP
;子程序名：DSTOV
;功能：把一个十进制数串转化为对应的数值
;入口参数：EDX=字符串开始地址偏移
;			CX=字符串长度
;出口参数：EAX=接收到的无符号整数
;		BL=0表示没有接收到数
;		1表示接收到的数符合要求
;		-1表示接收到的数无效
DSTOV 	PROC 	NEAR
		PUSH 	ESI
		MOV 	ESI,EDX
		MOV 	BL,0
		JCXZ	DTV3
		MOV 	BL,-1
		XOR 	EAX,EAX
		MOV 	EDX,10
		PUSH 	EDX
DTV1:
		MOV		DL,[ESI]
		INC 	ESI
		CMP 	DL,'0'
		JB 		SHORT DTV2
		CMP 	DL,'9'
		JA 		SHORT DTV2
		AND 	DL,0FH
		PUSH 	EDX
		MUL 	DWORD PTR [ESP+4]
		OR 		DL,DL
		POP 	EDX
		JNZ 	SHORT DTV2
		ADD 	EAX,EDX
		JC		DTV2
		LOOP	DTV1
		MOV		BL,1
DTV2:
		POP		EDX
DTV3:
		POP		ESI
		RET
DSTOV 	ENDP
;子程序名：HSTOV
;功能：把一个十六进制数串转化为对应的数值
;入口参数：EDX=字符串开始地址偏移
;		CX=字符串长度
;出口参数：EAX=接收到的无符号整数（其他略）
HSTOV 	PROC 	NEAR
		PUSH 	ESI
		MOV 	BL,0
		JCXZ	HTV4
		MOV 	BL,-1
		XOR 	EAX,EAX
		XOR 	ESI,ESI
HTV1: 
		MOV 	AL,[EDX]
		INC 	EDX
		BTS 	AX,5
		CMP 	AL,'0'
		JB 		SHORT HTV2
		CMP 	AL,'9'
		JA		HTV2
		AND		AL,0FH
		JMP 	SHORT HTV3
HTV2: 
		CMP 	AL,'a'
		JB 		SHORT HTV4
		CMP 	AL,'f'
		JA 		SHORT HTV4
		SUB 	AL,'a'-10
HTV3:
		TEST	ESI,0F0000000H
		JNZ 	SHORT HTV4
		SHL 	ESI,4
		ADD 	ESI,EAX
		LOOP 	HTVI
		MOV 	BL,1
HTV4: 
		MOV 	EAX,ESI
		POP 	ESI
		RET	
HSTOV 	ENDP
;子程序名：BSTOV
;功能：把一个二进制数串转化为对应的数值
;入口参数：EDX=字符串开始地址偏移
;		CX=字符串长度
;出口参数：EAX=接收到的无符号整数（其他略）
BSTOV 	PROC 	NEAR
		PUSH 	ESI
		MOV		BL,0
		JCXZ	BTV2
		MOV 	BL,-1
		XOR 	EAX,EAX
		XOR		ESI,ESI
		XCHG 	EDX,ESI
BTV1:
		MOV		AL,[ESI]
		INC 	ESI
		CMP 	AI,'0'
		JB 		SHORT BTV2
		CMP		AL,'1'
		JA 		SHORT BTV2
		AND		AL,0FH
		BT 		EDX,31
		JC 		SHORT BTV2
		SHI		EDX,1
		ADD 	EDX,EAX
		LOOP	BTV1
		MOV 	BL,1
BTV2:
		MOV		EAX,EDX
		POP		ESI
		RET
BSTOV 	ENDP
TOASC 	PROC 	NEAR
;内容略
TOASC 	ENDP
;
NEWLINE PROC NEAR
;内容略
NEWLINE ENDP
ECHO 	PROC NEAR
;内容略
ECHO 	ENDP
CSEG	ENDS
;
CSEG	SEGMENT USE16	;16位代码段
		ASSUME 	CS:CSEG
BEGIN: 
		MOV 	CX,2
@@1:
		MOV		AL,':'		;显示管号，提示输入
		CALL	ECHO
		CALL 	GETVAL		;接收字符串
		CMP		BL,0
		JZ 		OVER		;空串结束
		CMP 	BL,1
		JZ 		SHORT @@2	;正常，下一个
		MOV 	AL,'x'
		CALL 	ECHO		;无效，显示叉号
		CALL 	NEWLINE
		JMP		@@1
@@2: 
		PUSH 	EAX			;保存到堆栈
		LOOP	@@1
		MOV		AL,'='
		CALL 	ECHO		;显示等于号，表示乘积
		POP 	EAX
		POP 	EDX			;弹出两个数
		MUL 	EDX			;求乘积
		CALL 	LTODEC		;显示乘积
		CALL 	NEWLINE
		CALL 	NEWLINE
		JMP 	BEGIN
OVER:
		MOV		AH,4CH		;结束
		INT		21H
CSEG 	ENDS
		END 	BEGIN
```

